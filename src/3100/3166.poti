contem(texto, palavra: Texto) =
  texto.contains(palavra) ou texto.contains(palavra.inverta)

min(a, b: Inteiro) = se a < b entao a senao b fim

entrada = leia_inteiros(" ")
n, m, p = entrada[1], entrada[2], entrada[3]
palavras = leia_textos(n).mapeie(_.minusculo)
letras = leia_textos(m).mapeie(_.minusculo)
menor = min(m, p)
principal = para i de 1 ate menor gere letras[i][i] fim.junte("")
superior = para i de 1 ate p-6 gere para j de 1 ate min(menor-i, m) gere letras[j][i+j] fim.junte("") fim
inferior = para i de 1 ate m-6 gere para j de 1 ate min(menor-i, p) gere letras[i+j][j] fim.junte("") fim

para p em palavras faca
  se contem(principal, p) entao
    escreva "1 Palavra {'"'}{p}{'"'} na diagonal principal"
  senaose contem(superior.junte("|"), p) entao
    escreva "2 Palavra {'"'}{p}{'"'} acima da diagonal principal"
  senaose contem(inferior.junte("|"), p) entao
    escreva "3 Palavra {'"'}{p}{'"'} abaixo da diagonal principal"
  senao
    escreva "4 Palavra {'"'}{p}{'"'} inexistente"
  fim
fim
